DISTRO = "stereo-gadget"
DISTRO_NAME = "stereo-gadget"
DISTRO_VERSION = "1"

LICENSE_FLAGS_ACCEPTED = "synaptics-killswitch"

DISTRO_FEATURES = "\
usrmerge systemd \
ipv4 ipv6 wifi \
alsa \
usbhost \
bluetooth \
"

# Disables these distro features since we don't require them. See:
# https://docs.yoctoproject.org/5.0.5/singleindex.html#feature-backfilling
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit gobject-introspection-data"

VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"

# Minimize pipewire configuration. Avahi configuration would be required if we
# ever wanted to support Airplay reception.
PACKAGECONFIG:remove:pn-pipewire = " \
systemd-user-service \
flatpak \
pulseaudio jack gstreamer raop \
v4l2 libcamera webrtc-echo-cancelling \
gsettings avahi \
"

# Minimize bluez5 configuration.
PACKAGECONFIG:remove:pn-bluez5 = " \
obex-profiles \
network-profiles \
hid-profiles \
hog-profiles \
"

# RPi 4 bluetooth depends on hciattach to start up the Bluetooth modem, which
# is only built with --enable-tools and --enable-deprecated.
PACKAGECONFIG:append:pn-bluez5 = " tools"

PACKAGECONFIG:remove:pn-wireplumber = " systemd-user-service"

VIRTUAL-RUNTIME_wireplumber-config = "stereo-gadget-wp-config"
