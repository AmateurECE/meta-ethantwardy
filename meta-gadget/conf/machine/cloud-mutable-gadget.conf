require conf/machine/qemux86-64.conf

SUMMARY = "Linode cloud instance gadget"
DESCRIPTION = "This a/b mutable gadget is compatible with Linode cloud \
instances. It boots using a GRUB2 configuration, and each rootfs partition is \
a separate disk. Contrary to popular wisdom, a 'disk' in Linode is a \
container for a raw partition image, not a disk image (no partition table). \
In addition to the gadget image, the cloud-boot-image is also generated (to \
be written into the boot 'disk'). Ext4.gz images are generated, which can be \
uploaded as custom images in the Linode management portal."

IMAGE_FSTYPES = "ext4 ext4.gz"

MACHINEOVERRIDES =. "ab-mutable:qemux86-64:"

KMACHINE:cloud-mutable-gadget = "qemux86-64"

# Install kernel image and device tree into the rootfs (instead of the boot
# partition)
RDEPENDS:${KERNEL_PACKAGE_NAME}-base += " \
    kernel-image \
"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append = " \
    rauc \
    e2fsprogs \
    e2fsprogs-dumpe2fs \
    kernel-module-dm-verity \
    kernel-module-nbd \
    kernel-module-squashfs \
    grub-editenv \
"

# Add stuff needed for gadget rw data partition
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append = " \
    btrfs-tools \
    kernel-module-btrfs \
    kernel-module-overlay \
"

RAUC_CONFIG_FILE = "${@bb.utils.which(d.getVar('BBPATH'), 'rauc/cloud.conf')}"
