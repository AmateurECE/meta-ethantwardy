require conf/machine/rpi-gadget.conf
require conf/machine/ab-mutable.conf

# The boot partition contains only the things it needs to on these gadgets.
MACHINE_FEATURES += "minimal-bootpart"

# For this gadget, install only the required boot files, U-boot, and the boot
# script into the bootimg-partition. U-boot can use the same device tree blob
# as the kernel.
IMAGE_BOOT_FILES = " \
    ${BOOTFILES_DIR_NAME}/* \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
    bcm2711-rpi-4-b.dtb \
    boot.scr \
"

# Install kernel image and device tree into the rootfs (instead of the boot
# partition)
# TODO: Remove this when I'm ready to migrate mail.harmonyfoodsonline.com to the new boot scheme.
RDEPENDS:${KERNEL_PACKAGE_NAME}-base += "kernel-devicetree"

# The order of these matters. RAUC uses the first one as the image type to
# place in the bundle.
IMAGE_FSTYPES = "tar.bz2 wic.bz2 wic.bmap"
WKS_FILE = "ab-mutable-raspberrypi.wks"

# Add stuff needed for software update
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append = " \
    u-boot-fw-utils \
"

PREFERRED_PROVIDER_virtual/u-boot-env = "u-boot-env-ab-mutable"
IMAGE_BOOT_FILES += "uboot.env"

PREFERRED_PROVIDER_u-boot-default-script = "u-boot-rauc-boot"

RAUC_CONFIG_FILE = "${@bb.utils.which(d.getVar('BBPATH'), 'rauc/rpi.conf')}"
