require conf/machine/rpi-gadget.conf

MACHINEOVERRIDES =. "ab-mutable:"

# The boot partition contains only the things it needs to on these gadgets.
MACHINE_FEATURES += "minimal-bootpart"

# For this gadget, install only the required boot files, U-boot, and the boot
# script into the bootimg-partition. U-boot can use the same device tree blob
# as the kernel.
IMAGE_BOOT_FILES = " \
    ${BOOTFILES_DIR_NAME}/* \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
    bcm2711-rpi-4-b.dtb \
    boot.scr \
"

# Install kernel image and device tree into the rootfs (instead of the boot
# partition)
RDEPENDS:${KERNEL_PACKAGE_NAME}-base += "kernel-image kernel-devicetree"

IMAGE_FSTYPES = "wic.bz2 wic.bmap tar.bz2"
WKS_FILE = "ab-mutable-raspberrypi.wks"

# Add stuff needed for software update
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append = " \
    rauc \
    u-boot-fw-utils \
    e2fsprogs \
    kernel-module-dm-verity \
"

PREFERRED_PROVIDER_virtual/u-boot-env = "u-boot-env-ab-mutable"
IMAGE_BOOT_FILES += "uboot.env"
